apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.fox.ttrss"
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "boolean", "ENABLE_TRIAL", "true"
        buildConfigField "boolean", "ENABLE_UPDATER", "false"
        minSdkVersion 16
        targetSdkVersion 30
    }

    signingConfigs {
        fdroid {
            if (project.hasProperty("FDROID_STORE_FILE")) {
                storeFile file(FDROID_STORE_FILE)
                storePassword FDROID_STORE_PASSWORD
                keyAlias FDROID_KEY_ALIAS
                keyPassword FDROID_KEY_PASSWORD
            }
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    buildTypes {
        release {
            minifyEnabled  false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        fdroid {
            minifyEnabled  false
            versionNameSuffix "-fdroid"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            buildConfigField "boolean", "ENABLE_TRIAL", "false"
            buildConfigField "boolean", "ENABLE_UPDATER", "true"
            signingConfig signingConfigs.fdroid
            matchingFallbacks = ['release']
        }
    }
}

dependencies {
    implementation files('libs/dashclock-api-r1.1.jar')
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation('com.github.bumptech.glide:okhttp3-integration:4.12.0') {
        exclude group: 'glide-parent'
    }
    implementation 'org.jsoup:jsoup:1.12.2'
    implementation 'com.bogdwellers:pinchtozoom:0.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'jp.wasabeef:glide-transformations:4.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'com.otaliastudios:nestedscrollcoordinatorlayout:1.0.3'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'frankiesardo:icepick:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'
    implementation 'com.github.livefront:bridge:v1.1.1'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation 'com.github.javiersantos:AppUpdater:2.7'
}
